buildscript {
  ext.kotlinVersion = '1.3.70'
  ext.postgresqlVersion = '42.2.11'

  repositories {
    mavenCentral()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    classpath "org.postgresql:postgresql:$postgresqlVersion"
  }
}

plugins {
  id 'org.jetbrains.kotlin.jvm' version '1.3.70'
  id 'com.github.johnrengelman.shadow' version '5.2.0'
  id "org.jlleitschuh.gradle.ktlint" version '9.2.1'
  id "org.flywaydb.flyway" version "6.3.1"
}

group 'pl.edu.pk.backend'
version '1.0'

allprojects {
  apply plugin: 'idea'
  apply plugin: 'kotlin'

  repositories {
    mavenCentral()
  }

  ext {
    vertxVersion = "3.8.5"
    junitVersion = "5.6.0"
    assertJVersion = "3.15.0"
    mockkJVersion = "1.9.3"
    log4jVersion = "2.13.1"
  }

  tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions {
      jvmTarget = "1.8"
    }
  }
}

subprojects {
  apply plugin: 'org.jlleitschuh.gradle.ktlint'

  test {
    useJUnitPlatform()
  }
}

flyway {
  url = "jdbc:postgresql://${System.getenv('DATABASE_HOST')}/${System.getenv('DATABASE_NAME')}"
  user = System.getenv('DATABASE_USER')
  password = System.getenv('DATABASE_PASSWORD')
  locations = ["filesystem:${project.projectDir}/core/src/main/resources/db/migration"]
}
