apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'org.hidetake.swagger.generator'

mainClassName = 'pl.edu.pk.backend.MainKt'

dependencies {
  implementation project(":core")
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
  implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
  implementation "io.vertx:vertx-core:$vertxVersion"
  implementation "io.vertx:vertx-lang-kotlin:$vertxVersion"
  implementation "io.vertx:vertx-web:$vertxVersion"
  implementation "io.vertx:vertx-pg-client:$vertxVersion"
  implementation "io.vertx:vertx-auth-jwt:$vertxVersion"
  implementation "io.vertx:vertx-junit5:$vertxVersion"
  implementation "org.postgresql:postgresql:$postgresqlVersion"
  implementation "org.flywaydb:flyway-core:6.3.1"
  implementation "de.mkammerer:argon2-jvm:2.5"
  implementation "com.auth0:java-jwt:3.10.1"
  implementation "org.apache.logging.log4j:log4j-api:$log4jVersion"
  implementation "org.apache.logging.log4j:log4j-core:$log4jVersion"
  testImplementation "org.assertj:assertj-core:$assertJVersion"
  testImplementation "io.mockk:mockk:$mockkJVersion"
  testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
  testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
  testRuntime "org.junit.jupiter:junit-jupiter-engine:$junitVersion"

  swaggerUI "org.webjars:swagger-ui:3.17.1"
}

task copySwaggerUi {
  doLast {
    copy {
      into 'src/main/resources/webroot/swagger/'
      from(zipTree(configurations.swaggerUI.singleFile)) {
        exclude '**/*.gz'
        exclude '**/*.MF'
        exclude '**/*.xml'
        exclude '**/*.properties'
      }
      includeEmptyDirs = false
      filter {
        it.replaceAll('https://petstore.swagger.io/v2/swagger.json', '/api/api.yaml')
      }
      eachFile {
        path = name
      }
    }
  }
}

shadowJar.dependsOn copySwaggerUi
shadowJar {
  archiveName = "backend.jar"
  destinationDirectory = new File("${rootProject.projectDir}/target/")
  classifier = 'fat'
  from('src/main/resources') {
    include '**/*'
  }
}

clean {
  delete 'src/main/resources/webroot/swagger'
}
