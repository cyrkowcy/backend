apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'org.hidetake.swagger.generator'

mainClassName = 'pl.edu.pk.backend.MainKt'

dependencies {
  compile project(":core")
  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
  compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
  compile "io.vertx:vertx-core:$vertxVersion"
  compile "io.vertx:vertx-lang-kotlin:$vertxVersion"
  compile "io.vertx:vertx-web:$vertxVersion"
  compile "io.vertx:vertx-pg-client:$vertxVersion"
  compile "io.vertx:vertx-auth-jwt:$vertxVersion"
  compile "io.vertx:vertx-junit5:$vertxVersion"
  compile "de.mkammerer:argon2-jvm:2.5"
  compile "com.auth0:java-jwt:3.10.1"
  compile "org.apache.logging.log4j:log4j-api:$log4jVersion"
  compile "org.apache.logging.log4j:log4j-core:$log4jVersion"
  testCompile "org.assertj:assertj-core:$assertJVersion"
  testCompile "io.mockk:mockk:$mockkJVersion"
  testCompile "org.junit.jupiter:junit-jupiter-api:$junitVersion"
  testCompile "org.junit.jupiter:junit-jupiter-params:$junitVersion"
  testRuntime "org.junit.jupiter:junit-jupiter-engine:$junitVersion"

  swaggerUI "org.webjars:swagger-ui:3.17.1"
}

task copySwaggerUi {
  doLast {
    copy {
      into 'src/main/resources/webroot/swagger/'
      from(zipTree(configurations.swaggerUI.singleFile)) {
        exclude '**/*.gz'
        exclude '**/*.MF'
        exclude '**/*.xml'
        exclude '**/*.properties'
      }
      includeEmptyDirs = false
      filter {
        it.replaceAll('https://petstore.swagger.io/v2/swagger.json', '/api/api.yaml')
      }
      eachFile {
        path = name
      }
    }
  }
}

shadowJar.dependsOn copySwaggerUi
shadowJar {
  archiveName = "backend.jar"
  destinationDirectory = new File("${rootProject.projectDir}/target/")
  classifier = 'fat'
  from('src/main/resources') {
    include '**/*'
  }
}

clean {
  delete 'src/main/resources/webroot/swagger'
}
